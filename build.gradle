import org.ajoberstar.grgit.Grgit

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "org.ajoberstar.grgit" version "4.1.1"
}

group = 'se.inera.intyg.plugin.fitnesse'

version = System.properties['buildVersion'] ?: '1.1.0-SNAPSHOT'

configurations {
    deployerJars
}

gradlePlugin {
    plugins {
        intygPlugin {
            id = "se.inera.intyg.plugin.fitnesse"
            implementationClass = "se.inera.fitnesse.FitnessePlugin"
        }
    }
}

task install {
    description("Publishes artifact to local maven repository.")
    dependsOn(publishToMavenLocal)
}

task tagRelease {
    description = 'Tags the current head with the projects version.'
    doLast {
        def grgit = Grgit.open(creds: new org.ajoberstar.grgit.Credentials(System.properties['githubUser'],
                System.properties['githubPassword']))
        grgit.tag.add {
            name = "v" + version
            message = "Release of ${version}"
        }
        grgit.push(tags: true)
    }
}

publishing {
    repositories {
        maven {
            url = uri("https://nexus.drift.inera.se/repository/maven-releases/")
            credentials {
                username = System.properties['ineraNexusUsername']
                password = System.properties['ineraNexusPassword']
            }
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.12'
    implementation gradleApi()

    testImplementation 'org.spockframework:spock-core:2.2-groovy-2.5'

    deployerJars "org.apache.maven.wagon:wagon-webdav-jackrabbit:3.5.2"
}

repositories {
    mavenLocal()
    maven { url "https://nexus.drift.inera.se/repository/it-public/" }
    mavenCentral {
        content {
            // this repository contains everything BUT artifacts with group starting with "se.inera"
            excludeGroupByRegex "se\\.inera.*"
        }
    }
}

buildscript {
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-core:5.0.0'
    }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
