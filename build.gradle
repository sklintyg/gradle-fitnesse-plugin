apply plugin: 'groovy'
apply plugin: 'maven'

group = 'se.inera.intyg.plugin.fitnesse'

version = System.properties['buildVersion'] ?: '1.1-SNAPSHOT'

configurations {
    deployerJars
}

task tagRelease {
    description = 'Tags the current head with the projects version.'
    doLast {
        def grgit = org.ajoberstar.grgit.Grgit.open(creds: new org.ajoberstar.grgit.Credentials(System.properties['githubUser'],
                                                                                                System.properties['githubPassword']))
        grgit.tag.add {
            name = "v" + version
            message = "Release of ${version}"
        }
        grgit.push(tags: true)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
                authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
            }
        }
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.11"
    compile gradleApi()

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    deployerJars "org.apache.maven.wagon:wagon-webdav:1.0-beta-2"
}

repositories {
    maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
    mavenCentral()
}

buildscript {
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.+'
    }

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
