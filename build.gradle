apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'java-gradle-plugin'

group = 'se.inera.intyg.plugin.fitnesse'

version = System.properties['buildVersion'] ?: '1.1-SNAPSHOT'

configurations {
    deployerJars
}

gradlePlugin {
    plugins {
        intygPlugin {
            id = "se.inera.intyg.plugin.fitnesse"
            implementationClass = "se.inera.fitnesse.FitnessePlugin"
        }
    }
}

task tagRelease {
    description = 'Tags the current head with the projects version.'
    doLast {
        def grgit = org.ajoberstar.grgit.Grgit.open(creds: new org.ajoberstar.grgit.Credentials(System.properties['githubUser'],
                System.properties['githubPassword']))
        grgit.tag.add {
            name = "v" + version
            message = "Release of ${version}"
        }
        grgit.push(tags: true)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
                authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
            }
        }
    }
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.5.8"
    compile gradleApi()

    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'

    deployerJars "org.apache.maven.wagon:wagon-webdav:1.0-beta-2"
}

repositories {
    mavenLocal()
    maven { url "https://nexus.drift.inera.se/repository/it-public/" }
    jcenter {
        content {
            // this repository contains everything BUT artifacts with group starting with "se.inera"
            excludeGroupByRegex "se\\.inera.*"
        }
    }
}

buildscript {
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-core:3.1.1'
    }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
