import org.eclipse.jgit.api.Git
import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.transport.UsernamePasswordCredentialsProvider

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id "org.ajoberstar.grgit" version "4.1.1"
}

group = 'se.inera.intyg.plugin.fitnesse'
version = System.properties['buildVersion'] ?: '1.1.4-SNAPSHOT'

configurations { deployerJars }

gradlePlugin {
    plugins {
        intygPlugin {
            id = "se.inera.intyg.plugin.fitnesse"
            implementationClass = "se.inera.fitnesse.FitnessePlugin"
        }
    }
}

tasks.register('install') {
    description("Publishes artifact to local maven repository.")
    dependsOn(publishToMavenLocal)
}

tasks.register('tagRelease') {
    description = "Tags the current head with the projects version."
    doLast {
        def file = new File(project.rootProject.projectDir, ".git")
        def git = new Git(new FileRepositoryBuilder().setGitDir(file).readEnvironment().findGitDir().build())
        def credsProvider = new UsernamePasswordCredentialsProvider(System.getProperty("githubUser") as String,
                System.getProperty("githubPassword") as String)
        git.tag().setName("v${project.version}").setMessage("Release of ${project.version}").call()
        git.push().setCredentialsProvider(credsProvider).setPushAll().setPushTags().call()
    }
}

publishing {
    repositories {
        maven {
            url = uri("https://nexus.drift.inera.se/repository/maven-releases/")
            credentials {
                username = System.properties['ineraNexusUsername']
                password = System.properties['ineraNexusPassword']
            }
        }
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.17'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    deployerJars "org.apache.maven.wagon:wagon-webdav-jackrabbit:3.5.3"
}

repositories {
    mavenLocal()
    maven { url "https://nexus.drift.inera.se/repository/it-public/" }
    mavenCentral {
        content {
            excludeGroupByRegex "se\\.inera.*"
        }
    }
}
